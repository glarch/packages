# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname='spotify'
pkgver='1.1.10.546.ge08ef575'
_pkgrelease='19'
pkgrel='1'
pkgdesc='A music streaming client'
arch=('x86_64')
url='https://www.spotify.com'
license=('custom')
depends=(
    'alsa-lib'
    'at-spi2-atk'
    'libcurl-gnutls'
    'gconf'
    'gtk2'
    'nss'
    'libxss'
    'openssl-1.0'
    'xdg-utils'
)
optdepends=('ffmpeg' 'libnotify')
source=(
    'spotify.protocol'
    'LICENSE'
    'http://repository.spotify.com/dists/stable/non-free/binary-amd64/Packages'
    "https://repository-origin.spotify.com/pool/non-free/s/spotify-client/spotify-client_${pkgver}-${_pkgrelease}_amd64.deb"
)
sha512sums=(
    '999abe46766a4101e27477f5c9f69394a4bb5c097e2e048ec2c6cb93dfa1743eb436bde3768af6ba1b90eaac78ea8589d82e621f9cbe7d9ab3f41acee6e8ca20'
    '0bdeb5ff8589ede415cd465f81dbbbabff94702e440b5ee9421af1212047b1661ef57a85c3bdf6ba84dbea02079443a7a7847ef36f33e19288ee67fbeecf1b8a'
    '39074e57abb9a647b4b27cc09bf91dd80a16589c8503b17061c23277b7718e778834bb79c168c771b9615d9e247f1ce9c5c597a10db905874a5d5c79f1109856'
    'f004083eee00600a51e50d7dee03846293b5ccd6966786e91055564c7963917b9aaa917de41f299a0f5b7baecea5f466fb37722b8631743ff2ca15f43851f5f4'
)
validpgpkeys=('931FF8E79F0876134EDDBDCCA87FF9DF48BF1C90') # Spotify <tux@spotify.com>

prepare() {
    # Get checksum
    echo "$(awk '/MD5sum/{print $2}' Packages | awk 'NR==1') spotify-client_${pkgver}-${_pkgrelease}_amd64.deb" \
        > "spotify-client_${pkgver}-${_pkgrelease}_amd64.deb.md5sum"
    echo "$(awk '/SHA1/{print $2}' Packages | awk 'NR==1') spotify-client_${pkgver}-${_pkgrelease}_amd64.deb" \
        > "spotify-client_${pkgver}-${_pkgrelease}_amd64.deb.sha1sum"
    echo "$(awk '/SHA256/{print $2}' Packages | awk 'NR==1') spotify-client_${pkgver}-${_pkgrelease}_amd64.deb" \
        > "spotify-client_${pkgver}-${_pkgrelease}_amd64.deb.sha256sum"
    echo "$(awk '/SHA512/{print $2}' Packages | awk 'NR==1') spotify-client_${pkgver}-${_pkgrelease}_amd64.deb" \
        > "spotify-client_${pkgver}-${_pkgrelease}_amd64.deb.sha512sum"
    # Verify
    for _hash_type in md5sum sha1sum sha256sum sha512sum; do
        ${_hash_type} -c "spotify-client_${pkgver}-${_pkgrelease}_amd64.deb.${_hash_type}"
    done
    tar -xzf data.tar.gz -C "${srcdir}"
}

package() {
    # protocol file for KDE
    install -Dm644 "${srcdir}/${pkgname}.protocol" "${pkgdir}/usr/share/kservices5/${pkgname}.protocol"
    # License
    # https://www.spotify.com/legal/end-user-agreement
    install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    # Document
    install -Dm644 "${srcdir}/usr/share/doc/spotify-client/changelog.gz" "${pkgdir}/usr/share/doc/spotify-client/changelog.gz"
    rm "${srcdir}"/usr/share/doc/spotify-client/changelog.gz
    # Desktop entry file
    install -Dm644 "${srcdir}/usr/share/${pkgname}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
    # Icons
    for size in 16 22 24 32 48 64 128 256 512; do
        install -Dm644 "${srcdir}/usr/share/${pkgname}/icons/spotify-linux-${size}.png" "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/${pkgname}.png"
    done
    # Binary
    install -dm755 "${pkgdir}/opt/spotify"
    mv "${srcdir}/usr/share/spotify" "${pkgdir}/opt/"
    install -dm755 "${pkgdir}/usr/bin"
    ln -s ../../opt/spotify/spotify "${pkgdir}/usr/bin/spotify"
    # Remove unnecessary files
    rm "${pkgdir}/opt/spotify/spotify.desktop"
    rm -rf "${pkgdir}/opt/spotify/icons/"
    rm -rf "${pkgdir}/opt/spotify/apt-keys/"
}
