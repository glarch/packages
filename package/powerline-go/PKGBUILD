# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname=powerline-go
pkgver=1.12.1
pkgrel=2
pkgdesc='A Powerline style prompt for your shell'
arch=('x86_64')
url='https://github.com/justjanne/powerline-go'
license=('GPL3')
depends=('glibc')
makedepends=(
  'git'
  'go'
)
source=("https://gitlab.com/glarch/applications/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.bz2")
sha512sums=('5670888e2f6fb65c5e2bc6b2c49f0098a3ba293d9e05a7406ea098bd8780b6c4fcc0836ef937de3405d3cd7e4fcf4c1d4cf741bf55d6ff0775b6d490110d4459')

prepare() {
  export GOPATH="${srcdir}/go"
  mkdir -p "${GOPATH}/src"

  if [[ ! -e "${GOPATH}/src/${pkgname}-v${pkgver}" ]]; then
    ln -s "${srcdir}/${pkgname}-v${pkgver}" "${GOPATH}/src/"
  fi
}

build() {
  cd "${GOPATH}/src/${pkgname}-v${pkgver}"

  go get
  go build -buildmode=pie -ldflags '-w -s'
}

package() {
  cd "${srcdir}/${pkgname}-v${pkgver}"
  install -Dm755 "${pkgname}-v${pkgver}" "${pkgdir}/usr/bin/${pkgname}"
}
