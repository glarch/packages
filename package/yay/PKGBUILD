# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>
# Contributor: Jguer <joaogg3@gmail.com>

pkgname='yay'
pkgver='9.4.4'
pkgrel='1'
pkgdesc='Yet another yogurt. Pacman wrapper and AUR helper written in go.'
url='https://github.com/Jguer/yay'
arch=('x86_64')
license=('GPL3')
depends=('pacman>=5.2' 'sudo' 'git')
makedepends=('go-pie')
source=("https://github.com/Jguer/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('668102eedcbe69b0b9fc9c804dd62a1614fe002bec9f89422724bca864680b0670f00ab062a30b2511af3ecd61e6f286a0483d0b0da9277df6bec9b79cf471b2')
b2sums=('91f1a08f6c3f1cbbd556cda45f8fb5027f5ed2e1b2e5daff74be2d4b4073ef115be56c9f9cecacf47e392a13db8ddeb52e965de5113a5bec3aefeb808c648560')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}" || exit
    EXTRA_GOFLAGS="-gcflags all=-trimpath=${PWD} -asmflags all=-trimpath=${PWD}" \
        LDFLAGS="-linkmode external -extldflags \"${LDFLAGS}\"" \
        make VERSION="${pkgver}" DESTDIR="${pkgdir}" PREFIX='/usr'
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}" || exit
    make VERSION="${pkgver}" DESTDIR="${pkgdir}" PREFIX='/usr' install
}
