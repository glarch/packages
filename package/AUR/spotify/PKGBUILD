# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname=spotify
pkgver=1.1.5.153.gf614956d
_pkgrelease=16
pkgrel=1
pkgdesc='A music streaming client'
arch=('x86_64')
url='https://www.spotify.com'
license=('custom')
depends=(
  'alsa-lib'
  'at-spi2-atk'
  'gcc-libs'
  'libcurl-gnutls'
  'libx11'
  'libxtst'
  'gconf'
  'glib2'
  'gtk2'
  'nss'
  'libxss'
  'openssl-1.0'
  'xdg-utils'
)
optdepends=(
  'ffmpeg'
  'libnotify'
)
source=(
  'spotify.protocol'
  'LICENSE'
  'http://repository.spotify.com/dists/stable/non-free/binary-amd64/Packages'
  "https://repository-origin.spotify.com/pool/non-free/s/spotify-client/spotify-client_${pkgver}-${_pkgrelease}_amd64.deb"
)
sha512sums=(
  '999abe46766a4101e27477f5c9f69394a4bb5c097e2e048ec2c6cb93dfa1743eb436bde3768af6ba1b90eaac78ea8589d82e621f9cbe7d9ab3f41acee6e8ca20'
  '0bdeb5ff8589ede415cd465f81dbbbabff94702e440b5ee9421af1212047b1661ef57a85c3bdf6ba84dbea02079443a7a7847ef36f33e19288ee67fbeecf1b8a'
  'a726ce2c61863429e5e2b8e624f94401bbb95ba54bbb266d23a15b0b7c2eb97ef2347b2446beeb0c222afab1dee6406edf719f228fe53e1aea2587eca2cdb8e1'
  'db354f161c35b5f1a2d633282fad8fccf565e0061e7d258012f28422d766630391d47d4dafe22448fb60f850bf4ccbfdf82443cf107e03727c077b2c62e524be'
)
validpgpkeys=('931FF8E79F0876134EDDBDCCA87FF9DF48BF1C90') # Spotify <tux@spotify.com>

prepare() {
  # Get checksum
  echo "$(awk '/MD5sum/{print $2}' Packages | awk 'NR==1') spotify-client_${pkgver}-${_pkgrelease}_amd64.deb" \
    > "spotify-client_${pkgver}-${_pkgrelease}_amd64.deb.md5sum"
  echo "$(awk '/SHA1/{print $2}' Packages | awk 'NR==1') spotify-client_${pkgver}-${_pkgrelease}_amd64.deb" \
    > "spotify-client_${pkgver}-${_pkgrelease}_amd64.deb.sha1sum"
  echo "$(awk '/SHA256/{print $2}' Packages | awk 'NR==1') spotify-client_${pkgver}-${_pkgrelease}_amd64.deb" \
    > "spotify-client_${pkgver}-${_pkgrelease}_amd64.deb.sha256sum"
  echo "$(awk '/SHA512/{print $2}' Packages | awk 'NR==1') spotify-client_${pkgver}-${_pkgrelease}_amd64.deb" \
    > "spotify-client_${pkgver}-${_pkgrelease}_amd64.deb.sha512sum"

  # Verify
  for _hash_type in md5sum sha1sum sha256sum sha512sum; do
    ${_hash_type} -c spotify-client_${pkgver}-${_pkgrelease}_amd64.deb.${_hash_type}
  done
}

package() {
  tar -xzf data.tar.gz -C "${srcdir}"

  # protocol file for KDE
  install -Dm644 "${srcdir}/spotify.protocol" "${pkgdir}/usr/share/kservices5/spotify.protocol"
  # License
  # https://www.spotify.com/legal/end-user-agreement
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Document
  install -Dm644 "${srcdir}"/usr/share/doc/spotify-client/changelog.gz "${pkgdir}"/usr/share/doc/spotify-client/changelog.gz
  rm "${srcdir}"/usr/share/doc/spotify-client/changelog.gz
  # Desktop entry file
  install -Dm644 "${srcdir}"/usr/share/spotify/spotify.desktop "${pkgdir}"/usr/share/applications/spotify.desktop
  # Icons
  for size in 16 22 24 32 48 64 128 256 512; do
    install -Dm644 "${srcdir}"/usr/share/spotify/icons/spotify-linux-${size}.png "${pkgdir}"/usr/share/icons/hicolor/${size}x${size}/spotify.png
  done
  # Binary
  mkdir -p "${pkgdir}/opt/spotify"
  mv "${srcdir}/usr/share/spotify" "${pkgdir}/opt/"
  mkdir -p "${pkgdir}/usr/bin"
  ln -s ../../opt/spotify/spotify "${pkgdir}/usr/bin/spotify"

  # Remove
  rm "${pkgdir}"/opt/spotify/spotify.desktop
  rm -rf "${pkgdir}"/opt/spotify/icons/
  rm -rf "${pkgdir}"/opt/spotify/apt-keys/

  chmod -R go-w "${pkgdir}"
}
