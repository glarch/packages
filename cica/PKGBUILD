# Maintainer: glatan <glatan.edu@gmail.com>
# Contributor: yuhr <sdn.pate(at)gmail.com>

pkgname=cica
pkgver=4.0.0
pkgrel=1
pkgdesc="Composite font with Ubuntu Mono and Rounded Mgen+."
url="https://github.com/miiton/Cica"
license=("custom")
arch=("any")
depends=("fontconfig" "fontforge" "xorg-font-utils")
install=$pkgname.install
source=("cica.zip::https://gitlab.com/glarch/applications/cica/-/archive/v${pkgver}/Cica-v${pkgver}.zip"
        "dejavu-fonts-ttf.zip::http://sourceforge.net/projects/dejavu/files/dejavu/2.37/dejavu-fonts-ttf-2.37.zip"
        "hack.zip::https://github.com/source-foundry/Hack/releases/download/v3.003/Hack-v3.003-ttf.zip"
        "rounded-mgenplus.7z::https://osdn.jp/downloads/users/8/8598/rounded-mgenplus-20150602.7z"
        "ubuntu-font-family.zip::https://assets.ubuntu.com/v1/fad7939b-ubuntu-font-family-0.83.zip"
        "https://github.com/googlei18n/noto-emoji/raw/master/fonts/NotoEmoji-Regular.ttf"
        )
sha512sums=('3541939599c0e22df01915c931b89a9f52f7f1739f01a0b78e0513991548b36c9332d3bcdc18a225085f4b99c8e54244ede4d0a2a31b04ba9b91f1c75c4c399f'
            'f5628efe484b6220d0bf8177aa826c28e7a36f0bcca2019e057c20f5915d579057f931377ec686dbfeebef05fcf6453472be77a21ef282bd9d8d0eaf62549a49'
            '7f6a2353711c9da17ff6ce648a28ff038462fbb6f3be91eb5b7f797a7182bb21224942ea5964cc8add3912e18c7e6267d311c7674f45f4295edfe1a1f69f46d3'
            '75ac248098b82a32947115018cafe2aa622f135928e783980ebecbbac87e3e9ca37f500c00ce8ec3c0c92ff51fb6c17dbf003d532bbd22de6ef495bb2848b56e'
            'd9ab7afc8aab55810de6e403aa02c0118bb35d94844cc14bb07b71daef4c6d63a9c927f0e81a9f97d29904512d94fa0226fbff2341351ac000fc233d2dc55727'
            '51b01ab0794be9f92c59679f6d56d4ce09ed959daeb9ec945bb837eb15a82ab302e83b29aab1972ac9cb648f7196a5f5ff4488a4622b36bedbc9cd0cab6dc3de')

prepare() {
  # ビルドに必要なフォント移動先のディレクトリ
  export sourceFonts="${srcdir}/Cica-v${pkgver}/sourceFonts/"
  # souceFontsに必要なフォントを移動
  cp dejavu-fonts-ttf-2.37/ttf/DejaVuSansMono.ttf ${sourceFonts}
  cp dejavu-fonts-ttf-2.37/ttf/DejaVuSansMono-Bold.ttf ${sourceFonts}
  cp ttf/* ${sourceFonts}
  cp rounded-mgenplus-1m-regular.ttf ${sourceFonts}
  cp rounded-mgenplus-1m-bold.ttf ${sourceFonts}
  cp ubuntu-font-family-0.83/UbuntuMono-R.ttf ${sourceFonts}
  cp ubuntu-font-family-0.83/UbuntuMono-B.ttf ${sourceFonts}
  cp ../NotoEmoji-Regular.ttf ${sourceFonts}
}

build() {
  cd ./Cica-v$pkgver/
  fontforge -lang=py -script cica.py
  # ビルドしたフォントとライセンスの仮置き場
  mkdir -p ../tmp/
  cp ./dist/*.ttf ../tmp/
  cp ./LICENSE.txt ../tmp/
}

package() {
  # ビルド結果の仮置き場に移動
  cd ./tmp/

  install -dm755 "${pkgdir}"/usr/share/fonts/TTF
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/TTF/

  install -d "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm644 *.txt "${pkgdir}/usr/share/licenses/${pkgname}/"
}
