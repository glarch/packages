# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname='starship'
pkgver='0.25.2'
pkgrel='1'
pkgdesc='The cross-shell prompt for astronauts.'
arch=('x86_64')
url='https://starship.rs/'
license=('custom:ISC')
depends=('gcc-libs' 'zlib')
makedepends=('rust')
checkdepends=('rust')
optdepends=('powerline-font: powerline symbols for terminal')
install="${pkgname}.install"
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('3666dac377030dd5a4034cf9843aced5cba7f4e146d1c83e311fd277191decb04cb79e5f270257a9d1c1f136bd49ff50de2b1e4ebeab2cda4a14a016b7d0663a')

build() {
	cd "${pkgname}-${pkgver}"
	cargo build --release --locked
}

check() {
	cd "${pkgname}-${pkgver}"
	# "cargo test --release --locked" does not pass now
	cargo test --locked
}

package() {
	cd "${pkgname}-${pkgver}"
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
	install -Dm644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
