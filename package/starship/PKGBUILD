# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname='starship'
pkgver='0.33.1'
pkgrel='1'
pkgdesc='The cross-shell prompt for astronauts.'
arch=('x86_64')
url='https://starship.rs/'
license=('custom:ISC')
depends=('gcc-libs' 'zlib')
makedepends=('rust')
checkdepends=('rust')
optdepends=('powerline-font: powerline symbols for terminal')
install="${pkgname}.install"
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('b44b0283f0b37557545623b4ed9c24f3cd15f81507ddabd1358835165260ca30d185f34adde7b8698086e02b4f5fa480c3f150cec382d2f91ead968d5eab67d9')
b2sums=('475a67b7b4128a5bbc1b65657459b41c1fe595e46ea38de3a0b898482aa7da5b09a5c8042f6e3dfbba22f6bff0567102a3fc08b4f3f5762407b97a0fd50ac0e5')

build() {
	cd "${pkgname}-${pkgver}" || exit
	cargo build --release --locked
}

check() {
	cd "${pkgname}-${pkgver}" || exit
	cargo test --locked
}

package() {
	cd "${pkgname}-${pkgver}" || exit
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
