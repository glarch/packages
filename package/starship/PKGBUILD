# Maintainer: glatan <glatan [dot] edu [at] gmail [dot] com>

pkgname='starship'
pkgver='0.41.0'
pkgrel='2'
pkgdesc='The cross-shell prompt for astronauts.'
arch=('x86_64')
url='https://starship.rs/'
license=('custom:ISC')
depends=('gcc-libs' 'zlib')
makedepends=('rust')
checkdepends=('rust')
optdepends=('powerline-font: powerline symbols for terminal')
install="${pkgname}.install"
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('69895bcf3c480a2b1c2191340993993ec656782a21dec5020e37cff278ee9d65690be2572515e129743e2c56398c2e595e8ee2e4c48a5eacab9f20e8ceefbdc9')
b2sums=('be1c70c4434573c8d27827c51ce7f15c67400d3ca1f9e40a24cf1e558b377cda033bd5b647a16c3bea8cdf6e98be8c7ec389f3845ecc76a7f19ecd092047ca8b')

build() {
	cd "${pkgname}-${pkgver}" || exit
	cargo build --release --locked
}

check() {
	cd "${pkgname}-${pkgver}" || exit
	cargo test --release --locked
}

package() {
	cd "${pkgname}-${pkgver}" || exit
	install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
